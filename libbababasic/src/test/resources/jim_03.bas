05 REM Binary Tree Sort
10 REM 29-10-2023
15 N=7
20 DIM D(N)
25 DIM L%(N,2)
27 DIM S%(N)
30 DATA 5, 3, 11, 1, 12, 8, 5
35 FOR I%=0 TO N-1:READ D(I%):NEXT I%
40 FOR I%=0 TO N-1
50 A=D(I%):L%(I%,0)=0:L%(I%,1)=0
60 IF I%=0 THEN GOTO 110
70 R=0
80 IF A>D(R) THEN J=1 ELSE J=0
90 IF L%(R,J)>0 THEN R=L%(R,J):GOTO 80
100 L%(R,J)=I%
110 NEXT I%
150 FOR I%=0 TO N-1
160 PRINT I%," ", D(I%)," ",L%(I%,0)," ",L%(I%,1)
170 NEXT I%
200 SP=0:R=0
210 IF L%(R,0)>0 THEN SP=SP+1:S%(SP)=R:R=L%(R,0):GOTO 210
220 PRINT D(R)
230 IF L%(R,1)>0 THEN R=L%(R,1):GOTO 210
240 IF SP=0 THEN GOTO 260
250 R=S%(SP):SP=SP-1:GOTO 220
260 PRINT "end"
